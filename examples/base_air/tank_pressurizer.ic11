pin Pump1 d0;
pin PaInput1 d1;
pin PaOutput1 d2;

// TotalMoles = Volume(Liters) * Pressure(kPa) / Temperature(K) / GasConst(8.314)
// PumpPerTick(moles) = PumpLitersSetting * InputTotalMoles / InputVolume(Liters)

const GasConstant = 8.314;
const TargetPressure = 9500;

const MinPower = 0.05;

void Main()
{
    while(true)
    {
        yield;

        ControlPump(0, 1, 2, TargetPressure);
        // ControlPump(3, 4, 5, tp2);
    }
}

void ControlPump(pumpIdx, paInpIdx, paOutpIdx, targetPressure)
{
    if (Pins[paOutpIdx].Pressure == -1 | Pins[paInpIdx].Pressure == -1)
    {
        Pins[pumpIdx].On = false;
        return;
    }
    
    var delta = targetPressure - Pins[paOutpIdx].Pressure;
    var reverse = delta < 0;
    Pins[pumpIdx].Mode = reverse;

    var pumpPower = 0;
    if (reverse) {
        var transferMolesPerLiter = Pins[paOutpIdx].TotalMoles / Pins[paOutpIdx].Volume;
        var targetMoles = targetPressure / Pins[paOutpIdx].Pressure * Pins[paOutpIdx].TotalMoles;
        var targSubMoles = Pins[paOutpIdx].TotalMoles - targetMoles;

        pumpPower = targSubMoles / transferMolesPerLiter;
    } else {
        var transferMolesPerLiter = Pins[paInpIdx].TotalMoles / Pins[paInpIdx].Volume;

        var targetMoles = 0;

        if (Pins[paOutpIdx].Pressure > 0)
            targetMoles = targetPressure / Pins[paOutpIdx].Pressure * Pins[paOutpIdx].TotalMoles;
        else
            targetMoles = Pins[paOutpIdx].Volume * targetPressure / Pins[paInpIdx].Temperature / GasConstant;
        
        var targAddMoles = targetMoles - Pins[paOutpIdx].TotalMoles;    

        pumpPower = targAddMoles / transferMolesPerLiter;
    }

    if (pumpPower < MinPower)
    {
        Pins[pumpIdx].On = false;
        return;
    }

    Pins[pumpIdx].Setting = pumpPower;
    Pins[pumpIdx].On = true;
}